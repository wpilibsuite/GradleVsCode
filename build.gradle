plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "edu.wpi.first.wpilib.versioning.WPILibVersioningPlugin" version "2.0"
    id "com.gradle.plugin-publish" version "0.9.10"
}

// Ensure that the WPILibVersioningPlugin is setup by setting the release type, if releaseType wasn't
// already specified on the command line
if (!hasProperty('releaseType')) {
    WPILibVersion {
        releaseType = 'dev'
    }
}

ext.pubVersion = ''
if (project.hasProperty("publishVersion")) {
    pubVersion = project.publishVersion
} else {
    pubVersion = WPILibVersion.version
}

if (pubVersion == '') {
    pubVersion = '0.0.1-unknown'
}

group 'edu.wpi.first'
version pubVersion

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

dependencies {
    compile 'com.google.code.gson:gson:2.2.4'
}

publishing {
    publications {
        plugin(MavenPublication) {
            artifact(jar) {
                if (project.hasProperty('localPublish')) {
                    classifier null
                    groupId 'gradle.plugin.' + group
                }
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/wpilibsuite/gradle-jni'
    vcsUrl = 'https://github.com/wpilibsuite/gradle-jni'
    description = 'This plugin provides easy to use JNI support for gradle.'
    tags = ['groovy', 'jni', 'utils', 'maven', 'frc', 'wpilib']

    plugins {
        GradleVsCode {
            id = 'edu.wpi.first.GradleVsCode'
            displayName = 'Gradle VS Code'
        }
    }
}

gradlePlugin {
    plugins {
        GradleVsCode {
            id = 'edu.wpi.first.GradleVsCode'
            implementationClass = 'edu.wpi.first.vscode.GradleVsCode'
        }
    }
}

apply from: 'versions.gradle'

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}
